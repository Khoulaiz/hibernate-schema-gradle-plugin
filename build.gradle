buildscript {

    ext['gradle.props.version'] = '1.4.5'
    ext['bintray.version'] = '1.7'

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("net.saliman:gradle-properties-plugin:${property('gradle.props.version')}")
        classpath("com.jfrog.bintray.gradle:gradle-bintray-plugin:${property('bintray.version')}")
    }
}

ext['project.version'] = '1.0.1'

ext['hibernate.version'] = '5.2.2.Final'
ext['reflections.version'] = '0.9.10'

ext['junit.version'] = '4.12'
ext['spock.version'] = '1.0-groovy-2.4'

group 'com.github.deathman92.plugin'
version "${property('project.version')}"
description "Gradle plugin for generate DDL scripts from JPA entities"

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'net.saliman.properties'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext {
    bintrayUser = "${System.env['CI_BINTRAY_USER'] ?: property('bintray.user')}"
    bintrayKey = "${System.env['CI_BINTRAY_KEY'] ?: property('bintray.key')}"
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile("org.hibernate:hibernate-entitymanager:${property('hibernate.version')}")
    compile("org.reflections:reflections:${property('reflections.version')}")
    testCompile("junit:junit:${property('junit.version')}")
    testCompile gradleTestKit()
    testCompile("org.spockframework:spock-core:${property('spock.version')}") {
        exclude module: 'groovy-all'
    }
}

test {
    testLogging.showStandardStreams = true

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

task sourcesJar (type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "${project.group}"
            artifactId "${project.name}"
            version "${project.version}"

            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = "${bintrayUser}"
    key = "${bintrayKey}"
    publications = ["mavenJava"]
    pkg {
        repo = "maven-release"
        name = "${project.name}"
        desc = "${project.description}"
        licenses = ["MIT"]
        labels = ["hibernate", "schema", "ddl", "annotation"]
        websiteUrl = "https://github.com/deathman92/hibernate-schema-gradle-plugin"
        issueTrackerUrl = "https://github.com/deathman92/hibernate-schema-gradle-plugin/issues"
        vcsUrl = "https://github.com/deathman92/hibernate-schema-gradle-plugin.git"

        githubRepo = "deathman92/hibernate-schema-gradle-plugin"
        githubReleaseNotesFile = "README.md"

        version {
            name = "${project.version}"
            released = new Date()
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}